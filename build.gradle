buildscript {

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/plugins-release" }
        maven { url "https://dl.bintray.com/kotlin/dokka/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
//        classpath "io.spring.gradle:propdeps-plugin:0.0.9.RELEASE"
//        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_platform_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

/*
 * Copyright (c) 2016. Sunghyouk Bae <sunghyouk.bae@gmail.com>
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// nebula.kotlin plugin 을 지정하면, kotlin 관련 설정을 기본적으로 설정해준다. 특히 jvm-target 1.8 을 지정해 준다.
// apply plugin: 'nebula.kotlin'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'idea'

apply plugin: 'maven'
apply plugin: 'maven-publish'
//apply plugin: 'propdeps'
//apply plugin: 'propdeps-maven'
//apply plugin: 'propdeps-idea'

//apply plugin: 'org.junit.platform.gradle.plugin'


group = 'com.github.debop'
version = '1.3.0-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "joda-time:joda-time:$joda_time_version"

    implementation "io.reactivex.rxjava2:rxjava:$rxjava2_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$kotlinx_coroutines_version"

    testImplementation "org.slf4j:slf4j-api:1.7.25"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"

    testImplementation "io.github.microutils:kotlin-logging:1.4.6"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"

    // Only needed to run tests in an (IntelliJ) IDE(A) that bundles an older version
    testRuntime "org.junit.platform:junit-platform-launcher:$junit_platform_version"

    // To avoid compiler warnings about @API annotations in JUnit code
    testCompileOnly "org.apiguardian:apiguardian-api:1.0.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true
        apiVersion = "1.2"
        languageVersion = "1.2"
        suppressWarnings = false
    }
}

compileTestKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

idea {
    module {
        downloadSources = false
        downloadJavadoc = false
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

test {
    useJUnitPlatform()
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
    jdkVersion = 8
    //reportNotDocumented = false
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives dokkaJar
}

configurations {
    published
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "debop"
            name "Sunghyouk Bae"
            email "sunghyouk.bae@gmail.com"
        }
    }

    scm {
        url "https://github.com/debop/koda-time"
    }
}

publishing {
    publications {
        bintrayJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact dokkaJar

            groupId "${project.group}"
            artifactId "${project.name}"
            version "${project.version}"

            pom.withXml {
                def root = asNode()
                root.appendNode('description', "Kotlin extension functions for Java 8 Time and joda-time")
                root.appendNode('name', "${project.name}")
                root.appendNode("url", "https://github.com/debop/koda-time")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRY_KEY")
    publications = ['bintrayJava']

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = "${project.name}"
        userOrg = 'debop'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/debop/kodatime.git'
        labels = ['kotlin', 'koda-time', 'joda-time']

        version {
            name = "${project.version}"
            desc = "Kotlin extensions for joda-time"
            released = new Date()
            vcsTag = "${project.version}"
        }
    }
}

clean {
    delete "out/"
}

wrapper {
    gradleVersion = '4.8.1'
}


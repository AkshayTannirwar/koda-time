buildscript {

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/plugins-release" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev/" }
    }
    dependencies {
        classpath "com.netflix.nebula:nebula-kotlin-plugin:$kotlin_version"
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.7"
    }
}

group = 'com.github.debop'
version = '1.1.1'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'nebula.kotlin'
apply plugin: 'maven-publish'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'application'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        apiVersion = "1.1"
        languageVersion = "1.1"
        jvmTarget = "1.8"
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://dl.bintray.com/kotlin/kotlin-dev/" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"

    compile "joda-time:joda-time:$joda_time_version"
    compile "org.joda:joda-convert:$joda_convert_version"

    testCompile "org.slf4j:slf4j-api:$slf4j_version"
    testCompile "ch.qos.logback:logback-classic:$logback_version"

    testCompile "junit:junit:$junit_version"
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true
    options.compilerArgs << '-Xlint:unchecked'
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

compileJava.dependsOn processResources

artifacts {
    archives sourceJar, javadocJar
}

test {
    ignoreFailures true
}

defaultTasks 'clean', 'assemble'

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.debop'
            artifactId 'koda-time'
            version '1.1.1'

            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}

mainClassName = 'com.github.debop.kodatimes.AppKt'

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}